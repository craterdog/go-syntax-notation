!>
................................................................................
.    Copyright (c) 2009-2025 Crater Dog Technologies.  All Rights Reserved.    .
................................................................................
.  DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.               .
.                                                                              .
.  This code is free software; you can redistribute it and/or modify it under  .
.  the terms of The MIT License (MIT), as published by the Open Source         .
.  Initiative. (See https://opensource.org/license/MIT)                        .
................................................................................
<!

!>
BALI DOCUMENT NOTATION
This document contains a formal definition of the Bali Document Notation™ (Bali)
using Crater Dog Syntax Notation™ (CDSN).

For more information on CDSN see the wiki at:
  - https://github.com/craterdog/go-syntax-notation/wiki

┌──────────────────────────────────────────────────────────────────────────────┐
│                               RULE DEFINITIONS                               │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$Document: Notice? Component

$Notice: comment newline

$Component: Entity Parameters?

$Entity:
    Element
    String
    Range
    Collection
    Procedure

$Parameters:
    MultilineParameters
    InlineParameters

$MultilineParameters: "(" newline AnnotatedAssociation+ ")"

$AnnotatedAssociation: Association note?

$InlineParameters: "(" Association AdditionalAssociation* ")"

$AdditionalAssociation: "," Association

$Association: symbol ":" Component

$Element:
    angle
    boolean
    citation
    duration
    moment
    number
    pattern
    percentage
    probability
    resource

$String:
    binary
    bytecode
    name
    narrative
    quote
    symbol
    tag
    version

$Range: LowerBound Primitive ".." Primitive UpperBound

$LowerBound:
    LowerInclusion
    LowerExclusion

$LowerInclusion: "[|"

$LowerExclusion: "(|"

$Primitive:
    Element
    String

$UpperBound:
    UpperInclusion
    UpperExclusion

$UpperInclusion: "|]"

$UpperExclusion: "|)"

$Collection:
    MultilineAttributes
    MultilineValues  ! Must be after the multiline attributes.
    InlineAttributes
    InlineValues  ! Must be after the inline attributes.
    NoAttributes
    NoValues

$MultilineAttributes: "[" newline AnnotatedAssociation+ "]"

$InlineAttributes: "[" Association AdditionalAssociation* "]"

$NoAttributes: "[" ":" "]"

$MultilineValues: "[" newline AnnotatedValue+ "]"

$AnnotatedValue: Component note?

$InlineValues: "[" Component AdditionalValue* "]"

$AdditionalValue: "," Component

$NoValues: "[" "]"

$Procedure:
    MultilineStatements
    InlineStatements
    NoStatements

$MultilineStatements: "{" newline AnnotatedStatement* "}"

$AnnotatedStatement:
    CommentLine
    StatementLine

$CommentLine: comment

$StatementLine: Statement note?

$InlineStatements: "{" Statement AdditionalStatement* "}"

$AdditionalStatement: ";" Statement

$NoStatements: "{" "}"

$Statement: MainClause OnClause?

$MainClause:
    Flow
    Induction
    Messaging
    Repository

$OnClause: "on" Failure MatchHandler+

$MatchHandler: "matching" Template "do" Procedure

$Failure: symbol

$Template: Expression

$Flow:
    IfClause
    SelectClause
    WhileClause
    WithClause
    ContinueClause
    BreakClause
    ReturnClause
    ThrowClause

$Induction:
    DoClause
    LetClause

$Messaging:
    PostClause
    RetrieveClause
    AcceptClause
    RejectClause
    PublishClause

$Repository:
    CheckoutClause
    SaveClause
    DiscardClause
    NotarizeClause

$IfClause: "if" Condition "do" Procedure

$Condition: Expression

$SelectClause: "select" Target MatchHandler+

$Target:
    Function
    Method
    Subcomponent
    Variable  ! This must be last since others also begin with an identifier.

$Function: identifier "(" Arguments? ")"

$Arguments: Argument AdditionalArgument*

$AdditionalArgument: "," Argument

$Argument: identifier

$Method: identifier Threading identifier "(" Arguments? ")"

$Threading:
    Dot
    Arrow

$Dot: "."

$Arrow: "<-"

$Subcomponent: identifier "[" Indices "]"

$Indices: Index AdditionalIndex*

$AdditionalIndex: "," Index

$Index: Expression

$Variable: identifier

$WhileClause: "while" Condition "do" Procedure

$WithClause: "with" "each" Item "in" Sequence "do" Procedure

$Item: symbol

$Sequence: Expression

$ContinueClause: "continue" "loop"

$BreakClause: "break" "loop"

$ReturnClause: "return" Result

$Result: Expression

$ThrowClause: "throw" Exception

$Exception: Expression

$DoClause: "do" Invocation

$Invocation:
    Function
    Method

$LetClause: "let" Recipient Assign Expression

$Assign:
    ColonEqual
    DefaultEqual
    PlusEqual
    DashEqual
    StarEqual
    SlashEqual

$ColonEqual: ":="

$DefaultEqual: "?="

$PlusEqual: "+="

$DashEqual: "-="

$StarEqual: "*="

$SlashEqual: "/="

$Recipient:
    Item
    Subcomponent

$PostClause: "post" Message "to" Bag

$Message: Expression

$Bag: Expression

$RetrieveClause: "retrieve" Recipient "from" Bag

$AcceptClause: "accept" Message

$RejectClause: "reject" Message

$PublishClause: "publish" Event

$Event: Expression

$CheckoutClause: "checkout" Recipient AtLevel? "from" Citation

$AtLevel: "at" "level" Expression

$Citation: Expression

$SaveClause: "save" Draft "as" Citation

$Draft: Expression

$DiscardClause: "discard" Draft

$NotarizeClause: "notarize" Draft "as" Citation

$Expression: Subject Predicate*

$Subject:
    Component
    Subcomponent
    Precedence
    Referent
    Complement
    Inversion
    Magnitude
    Function
    Method
    Variable  ! This must be last since others also begin with an identifier.

$Predicate: Operator Expression

$Operator:
    Plus
    Dash
    Star
    Slash
    Double
    Caret
    Less
    Equal
    More
    Ampersand
    Is
    Matches
    And
    San
    Ior
    Xor

$Plus: "+"

$Dash: "-"

$Star: "*"

$Slash: "/"

$Double: "//"

$Caret: "^"

$Less: "<"

$Equal: "="

$More: ">"

$Ampersand: "&"

$Is: "is"

$Matches: "matches"

$And: "and"

$San: "san"

$Ior: "ior"

$Xor: "xor"

$Precedence: "(" Expression ")"

$Referent: "@" Indirect

$Indirect:
    Component
    Subcomponent
    Referent
    Function
    Method
    Variable  ! This must be last since others also begin with an identifier.

$Complement: "not" Logical

$Logical:
    Component
    Subcomponent
    Precedence
    Referent
    Complement
    Function
    Method
    Variable  ! This must be last since others also begin with an identifier.

$Inversion: Inverse Numerical

$Inverse:
    Dash
    Slash
    Star

$Numerical:
    Component
    Subcomponent
    Precedence
    Referent
    Inversion
    Magnitude
    Function
    Method
    Variable  ! This must be last since others also begin with an identifier.

$Magnitude: "|" Numerical "|"

!>
┌──────────────────────────────────────────────────────────────────────────────┐
│                            EXPRESSION DEFINITIONS                            │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$alpha: ['A'..'Z' 'a'..'z']

$alphanumeric: alpha | base10

$angle: '~' ('0' | amplitude | "pi" | "π" | "tau" | "τ")

$authority: ~['/' CONTROL]+

$base10: ['0'..'9']

$base16: base10 | ['a'..'f']

$base32: base10 | ['A'..'D' 'F'..'H' 'J'..'N' 'P'..'T' 'V'..'Z']

$base64: alphanumeric | ['+' '/']

$binary: "'>" (EOL block+)? ' '* "<'"

$block: ' '* base64{1..60} EOL

$boolean: "false" | "true"

$bytecode: "'" (instruction (' ' instruction)*)* "'"

$character: escape | ~['"' CONTROL]

$citation: name '/' version

$comment: "!>" EOL (ANY | EOL)* EOL ' '* "<!"  ! Chooses the shortest match.

$day: (['0'..'2'] ['1'..'9']) | ('3' ['0'..'1'])

$days: timespan 'D'

$duration: '~' sign? 'P' (weeks | (years? months? days? ('T' hours? minutes? seconds?)?))

$escape: '\' (unicode | ['a' 'b' 'f' 'n' 'r' 't' 'v' '"' '\'])

$exponent: 'E' sign? ordinal

$float: sign? amplitude

$fraction: '.' base10+

$fragment: ~['>' CONTROL]*

$hour: (['0'..'1'] ['0'..'9']) | ('2' ['0'..'3'])

$hours: timespan 'H'

$imaginary: sign? amplitude 'i'

$infinity: sign? ("infinity" | '∞')

$instruction: base16{4}

$letter: LOWER | UPPER

$amplitude: ('0' fraction | ordinal fraction?) exponent?

$minute: ['0'..'5'] ['0'..'9']

$minutes: timespan 'M'

$moment: '<' sign? year ('-' month ('-' day ('T' hour (':' minute (':' second fraction?)?)?)?)?)? '>'

$month: ('0' ['1'..'9']) | ('1' ['0'..'2'])

$months: timespan 'M'

$name: ('/' identifier)+

$narrative: '"' '>' EOL (ANY | EOL)* EOL ' '* '<' '"'  ! Chooses the shortest match.

$note: "! " ~[CONTROL]*

$ordinal: ['1'..'9'] base10*

$path: ~['?' '#' '>' CONTROL]*

$pattern: "none" | regex | "any"

$percentage: real '%'

$probability: fraction | "one"

$polar: amplitude "e^" angle? 'i'

$query: ~['#' '>' CONTROL]*

$quote: '"' character* '"'

$real: '0' | float | infinity | undefined

$rectangular: float sign float? 'i'

$regex: '"' character+ '"' '?'

$resource: '<' scheme ':' ("//" authority)? '/' path ('?' query)? ('#' fragment)? '>'

$scheme: alpha (alphanumeric | '+' | '-' | '.')*

$second: (['0'..'5'] ['0'..'9']) | ('6' ['0'..'1'])

$seconds: timespan 'S'

$sign: '+' | '-'

$symbol: '$' identifier

$tag: '#' base32+

$timespan: '0' | (ordinal fraction?)

$undefined: "undefined"

$unicode: ('u' base16{4}) | ('U' base16{8})

$version: 'v' ordinal ('.' ordinal)*

$weeks: timespan 'W'

$year: '0' | ordinal

$years: timespan 'Y'

$number: polar | rectangular | imaginary | real  ! Must be after percentage.

$identifier: letter (letter | DIGIT)*  ! Must be after other identifiers.

