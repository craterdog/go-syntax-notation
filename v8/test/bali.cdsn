!>
................................................................................
.    Copyright (c) 2009-2026 Crater Dog Technologies™.  All Rights Reserved.   .
................................................................................
.  DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.               .
.                                                                              .
.  This code is free software; you can redistribute it and/or modify it under  .
.  the terms of The MIT License (MIT), as published by the Open Source         .
.  Initiative. (See https://opensource.org/license/MIT)                        .
................................................................................
<!

!>
BALI DOCUMENT NOTATION
This document contains a formal definition of the Bali Document Notation™ (Bali)
using Crater Dog Syntax Notation™ (CDSN).

For more information on CDSN see the wiki at:
  - https://github.com/craterdog/go-syntax-notation/wiki

┌──────────────────────────────────────────────────────────────────────────────┐
│                               RULE DEFINITIONS                               │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$Document: Heading? Component

$Heading: comment

$Component: Entity Generics?

$Entity:
    Element
    Sequence
    Range
    Collection
    Procedure

$Generics: "(" Parameter+ ")"

$Parameter: symbol ":" Constraint

$Constraint: Literal Generics?

$Literal:
    Element
    Sequence
    Range

$Element:
    angle
    boolean
    duration
    glyph
    moment
    percentage
    number
    probability
    resource

$Sequence:
    binary
    bytecode
    name
    narrative
    pattern
    quote
    symbol
    tag
    version

$Range: Left Primitive? ".." Primitive? Right

$Left:
    "["
    "("

$Right:
    "]"
    ")"

$Primitive:
    Element
    Sequence

$Collection:
    Empty
    Attributes
    Items  ! Must be after attributes.

$Empty: "[:]"

$Attributes: "[" Association+ "]"

$Association: Primitive ":" Entry

$Items: "[" Entry* "]"

$Entry: Component note?

$Procedure: "{" Line* "}"

$Line:
    Annotation
    Statement

$Annotation:
    note
    comment

$Statement: MainClause OnClause?

$MainClause:
    FlowControl
    LocalTransformation
    MessageHandling
    RepositoryAccess

$OnClause: "on" symbol MatchingClause+

$MatchingClause: "matching" Expression "do" Procedure

$FlowControl:
    IfClause
    SelectClause
    WhileClause
    WithClause
    ContinueClause
    BreakClause
    ReturnClause
    ThrowClause

$LocalTransformation:
    DefineClause
    AssignClause
    InvokeClause

$MessageHandling:
    SendClause
    ReceiveClause
    AcceptClause
    RejectClause
    PublishClause

$RepositoryAccess:
    SaveClause
    RetrieveClause
    DiscardClause
    NotarizeClause
    InspectClause
    CheckoutClause

$IfClause: "if" Expression "do" Procedure

$SelectClause: "select" Expression MatchingClause+

$WhileClause: "while" Expression "do" Procedure

$WithClause: "with" "each" symbol "in" Expression "do" Procedure

$ContinueClause: "continue" "loop"

$BreakClause: "break" "loop"

$ReturnClause: "return" Expression

$ThrowClause: "throw" Expression

$DefineClause: "define" Constant "as" Expression

$Constant: symbol

$AssignClause: "assign" Recipient Assignment Expression

$Recipient:
    Variable
    Subcomponent

$Variable: symbol

$Subcomponent: identifier "[" Index+ "]"

$Index:
    Value
    Primitive

$Value: identifier

$Assignment:
    "?="  ! Assign a default value if not already initialized.
    ":="
    "+="
    "-="
    "*="
    "/="
    "&="

$InvokeClause: "invoke" Method

$Method: identifier Invocation identifier "(" Argument* ")"

$Invocation:
    "<-"  ! The method is invoked synchronously.
    "<~"  ! The method is invoked asynchronously.

$Argument:
    Value
    Literal

$SendClause: "send" Message "to" Bag

$ReceiveClause: "receive" Recipient "from" Bag  ! Returns a message.

$AcceptClause: "accept" Message "from" Bag

$RejectClause: "reject" Message "from" Bag

$PublishClause: "publish" Message

$Message: Expression

$Bag: Expression  ! The global name of the message bag.

$SaveClause: "save" Draft "to" Recipient  ! The recipient receives a citation.

$RetrieveClause: "retrieve" Recipient "from" Citation

$DiscardClause: "discard" Citation

$Draft: Expression

$Citation: Expression  ! A citation to a draft document.

$NotarizeClause: "notarize" Draft "as" Location  ! The draft becomes a document.

$InspectClause: "inspect" Recipient "at" Location

$CheckoutClause: "checkout" Recipient AtLevel? "from" Location  ! Returns a draft.

$Location: Expression  ! The global name of a document.

$AtLevel: "at" "level" Expression

$Expression: Subject Predicate*

$Subject:
    Component
    Subcomponent
    Precedence
    Referent
    Complement
    Inversion
    Magnitude
    Function
    Method
    Value  ! This must be last since others also begin with an identifier.

$Precedence: "(" Expression ")"

$Referent: "@" Reference

$Reference:
    Component
    Subcomponent
    Referent
    Function
    Method
    Value  ! This must be last since others also begin with an identifier.

$Complement: "not" Reversible

$Reversible:
    Component
    Subcomponent
    Precedence
    Referent
    Complement
    Function
    Method
    Value  ! This must be last since others also begin with an identifier.

$Inversion: Inverse Numerical

$Inverse:
    "-"
    "/"
    "*"

$Numerical:
    Component
    Subcomponent
    Precedence
    Referent
    Inversion
    Magnitude
    Function
    Method
    Value  ! This must be last since others also begin with an identifier.

$Magnitude: "|" Expression "|"

$Function: identifier "(" Argument* ")"

$Predicate: Operator Expression

$Operator:
    Comparison
    Logical
    Arithmetic
    Lexical

$Comparison:
    "<"
    "="
    ">"
    "is"
    "matches"

$Logical:
    "and"
    "san"
    "ior"
    "xor"

$Arithmetic:
    "+"
    "-"
    "*"
    "/"
    "%"
    "^"

$Lexical:
    "&"

!>
┌──────────────────────────────────────────────────────────────────────────────┐
│                            EXPRESSION DEFINITIONS                            │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$angle: '~' ('0' | AMPLITUDE)

$binary: "'>" (EOL (SPACE* BASE64{2..60} EOL)+ SPACE*)? "<'"

$boolean: "false" | "true"

$bytecode: "'>" EOL (SPACE* INSTRUCTION{1..12} EOL)+ SPACE* "<'"

$comment: "!>" EOL (ANY | EOL)+ EOL SPACE* "<!"  ! Chooses the shortest match.

$duration: "~P" (WEEKS | (YEARS? MONTHS? DAYS? ('T' HOURS? MINUTES? SECONDS?)?))

$glyph: "'" (ESCAPE | ~[CONTROL]) "'"

$moment: '<' SIGN? YEAR '-' MONTH '-' DAY ('T' HOUR (':' MINUTE (':' SECOND FRACTION?)?)?)? '>'

$name: ('/' FOLDER)+

$narrative: "\">" EOL (ANY | EOL)+ EOL SPACE* "<\""  ! Chooses the shortest match.

$note: "! " ~[CONTROL]*

$pattern: "none" | REGEX | "any"

$percentage: REAL '%'

$probability: 'p' ('0' FRACTION? | "1")

$quote: '"' CHARACTER* '"'

$resource: '<' SCHEME ':' ("//" AUTHORITY)? '/' PATH ('?' QUERY)? ('#' FRAGMENT)? '>'

$symbol: '$' identifier

$tag: '#' BASE32{13..}

$version: 'v' ORDINAL ('.' ORDINAL)*

$number: POLAR | RECTANGULAR | IMAGINARY | REAL  ! Must be after other numerics.

$identifier: LETTER (LETTER | DIGIT | '-')*  ! Must be after other alphanumerics.

!>
┌──────────────────────────────────────────────────────────────────────────────┐
│                             FRAGMENT DEFINITIONS                             │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$ALPHA: ['A'..'Z' 'a'..'z']

$ALPHANUMERIC: ALPHA | BASE10

$AMPLITUDE: ('0' FRACTION | ORDINAL FRACTION?) EXPONENT? | TRANSCENDENTAL

$AUTHORITY: ~['/' CONTROL]+

$BASE10: ['0'..'9']

$BASE16: BASE10 | ['a'..'f']

$BASE32: BASE10 | ['A'..'D' 'F'..'H' 'J'..'N' 'P'..'T' 'V'..'Z']

$BASE64: ALPHANUMERIC | ['+' '/']

$CHARACTER: ESCAPE | '\' '"' | ~['"' CONTROL]

$DAY: (['0'..'2'] ['1'..'9']) | ('3' ['0'..'1'])

$DAYS: TIMESPAN 'D'

$ESCAPE: '\' (UNICODE | ['a' 'b' 'f' 'n' 'r' 't' 'v' '\'])

$EXPONENT: 'E' SIGN? ORDINAL

$FLOAT: SIGN? AMPLITUDE

$FOLDER: ALPHANUMERIC (('-' | '.')? ALPHANUMERIC)+

$FRACTION: '.' BASE10+

$FRAGMENT: ~['>' CONTROL]*

$HOUR: (['0'..'1'] ['0'..'9']) | ('2' ['0'..'3'])

$HOURS: TIMESPAN 'H'

$IMAGINARY: FLOAT 'i'

$INFINITY: SIGN? ("infinity" | '∞')

$INSTRUCTION: ':' BASE16{4}

$LETTER: LOWER | UPPER

$MINUTE: ['0'..'5'] ['0'..'9']

$MINUTES: TIMESPAN 'M'

$MONTH: ('0' ['1'..'9']) | ('1' ['0'..'2'])

$MONTHS: TIMESPAN 'M'

$ORDINAL: ['1'..'9'] BASE10*

$PATH: ~['?' '#' '>' CONTROL]*

$POLAR: AMPLITUDE "e^~" AMPLITUDE 'i'

$QUERY: ~['#' '>' CONTROL]*

$REAL: FLOAT | '0' | INFINITY | UNDEFINED

$RECTANGULAR: SIGN? AMPLITUDE SIGN AMPLITUDE 'i'

$REGEX: '"' CHARACTER+ '"' '?'

$SCHEME: ALPHA (ALPHANUMERIC | '+' | '-' | '.')*

$SECOND: (['0'..'5'] ['0'..'9']) | ('6' ['0'..'1'])

$SECONDS: TIMESPAN 'S'

$SIGN: '+' | '-'

$SPACE: ' '+

$TIMESPAN: '0' | (ORDINAL FRACTION?)

$TRANSCENDENTAL: "e" | "pi" | 'π' | "tau" | 'τ' | "phi" | 'φ'

$UNDEFINED: "undefined"

$UNICODE: ('u' BASE16{4}) | ('U' BASE16{8})

$WEEKS: TIMESPAN 'W'

$YEAR: '0' | ORDINAL

$YEARS: TIMESPAN 'Y'

