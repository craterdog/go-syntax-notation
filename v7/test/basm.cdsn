!>
................................................................................
.    Copyright (c) 2009-2025 Crater Dog Technologies.  All Rights Reserved.    .
................................................................................
.  DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.               .
.                                                                              .
.  This code is free software; you can redistribute it and/or modify it under  .
.  the terms of The MIT License (MIT), as published by the Open Source         .
.  Initiative. (See https://opensource.org/license/MIT)                        .
................................................................................
<!

!>
BALI ASSEMBLY LANGUAGE
This document contains a formal definition of the Bali Assembly Language™
using Crater Dog Syntax Notation™ (CDSN).

For more information on CDSN see the wiki at:
  - https://github.com/craterdog/go-syntax-notation/wiki

┌──────────────────────────────────────────────────────────────────────────────┐
│                               RULE DEFINITIONS                               │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$Assembly: Instruction+

$Instruction: Prefix? Action

$Prefix: label ":"

$Action:
    Note
    Skip
    Jump
    Push
    Pull
    Load
    Save
    Drop
    Call
    Send

$Note: "NOTE" comment

$Skip: "SKIP"

$Jump: "JUMP TO" label Conditionally?

$Conditionally:
    "ON NONE"
    "ON FALSE"
    "ON EMPTY"

$Push: "PUSH" Source

$Source:
    Literal
    Constant
    Argument
    Handler

$Literal: "LITERAL" quoted

$Constant: "CONSTANT" symbol

$Argument: "ARGUMENT" symbol

$Handler: "HANDLER" label

$Pull: "PULL" Value

$Value:
    "COMPONENT"
    "RESULT"
    "EXCEPTION"
    "HANDLER"

$Load: "LOAD" Component symbol

$Save: "SAVE" Component symbol

$Drop: "DROP" Component symbol

$Component:
    "DRAFT"
    "CONTRACT"
    "VARIABLE"
    "MESSAGE"

$Call: "CALL" symbol Cardinality?

$Cardinality:
    "WITH 1 ARGUMENT"
    "WITH 2 ARGUMENTS"
    "WITH 3 ARGUMENTS"

$Send: "SEND" symbol "TO" Destination Parameterized?

$Destination:
    "COMPONENT"
    "CONTRACT"

$Parameterized: "WITH ARGUMENTS"

!>
┌──────────────────────────────────────────────────────────────────────────────┐
│                            EXPRESSION DEFINITIONS                            │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$comment: "--" ~[CONTROL]*

$label: (ORDINAL '.')+ IDENTIFIER

$quoted: '`' ('\' '`' | ~['`'])+ '`'

$symbol: '$' IDENTIFIER ('-' ORDINAL)?

!>
┌──────────────────────────────────────────────────────────────────────────────┐
│                             FRAGMENT DEFINITIONS                             │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$IDENTIFIER: (LOWER | UPPER) (DIGIT | LOWER | UPPER)*

$ORDINAL: ['1'..'9'] DIGIT*

$SPACE: ' '+

