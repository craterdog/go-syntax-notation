!>
................................................................................
.    Copyright (c) 2009-2025 Crater Dog Technologies.  All Rights Reserved.    .
................................................................................
.  DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.               .
.                                                                              .
.  This code is free software; you can redistribute it and/or modify it under  .
.  the terms of The MIT License (MIT), as published by the Open Source         .
.  Initiative. (See https://opensource.org/license/MIT)                        .
................................................................................
<!

!>
CRATER DOG SYNTAX NOTATION
This document contains a formal definition of the Crater Dog Syntax Notation™
(CDSN) using CDSN itself in homage to Douglas Hofstadter of Gödel, Escher, Bach
fame.

For more information on CDSN see the wiki at:
  - https://github.com/craterdog/go-syntax-notation/wiki

┌──────────────────────────────────────────────────────────────────────────────┐
│                               RULE DEFINITIONS                               │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$Syntax: LegalNotice comment Rule+ comment Expression+

$LegalNotice: comment

$Rule: "$" uppercase ":" Definition

$Definition:
    LiteralAlternatives
    TokenAlternatives
    RuleAlternatives
    TermSequence  ! This must be last since it skips newlines.

$LiteralAlternatives: LiteralValue+

$LiteralValue: newline literal note?

$TokenAlternatives: TokenName+

$TokenName: newline lowercase note?

$RuleAlternatives: RuleName+

$RuleName: newline uppercase note?

$TermSequence: RuleTerm+ note?

$RuleTerm: Component Cardinality?  ! The default cardinality is one.

$Component:
    literal
    lowercase
    uppercase

$Cardinality:
    Constrained
    Quantified

$Constrained:
    "?"  ! Zero or one.
    "*"  ! Zero or more.
    "+"  ! One or more.

$Quantified: "{" number Limit? "}"  ! A quantified range of numbers is inclusive.

$Limit: ".." number?  ! A quantified range may have no upper limit.

$Expression: "$" lowercase ":" Pattern

$Pattern: Alternatives note?

$Alternatives: Sequence AlternativeSequence*

$AlternativeSequence: "|" Sequence

$Sequence: Repetition+

$Repetition: Element Cardinality?  ! The default cardinality is exactly one.

$Element:
    Group
    Filter
    Text

$Group: "(" Alternatives ")"

$Filter: "~"? "[" Character+ "]"

$Character:
    Implicit
    Explicit

$Implicit: intrinsic

$Explicit: glyph Extent?  ! An explicit range of glyphs is inclusive.

$Extent: ".." glyph

$Text:
    glyph
    literal
    lowercase
    intrinsic

!>
┌──────────────────────────────────────────────────────────────────────────────┐
│                            EXPRESSION DEFINITIONS                            │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$base16: ['0'..'9' 'a'..'f']

$comment: "!>" EOL (ANY | EOL)* EOL "<!" EOL  ! Chooses the shortest possible match.

$escape: '\' (unicode | ['a' 'b' 'f' 'n' 'r' 't' 'v' '"' '\'])

$glyph: "'" ~[CONTROL] "'"  ! Any printable unicode character.

$intrinsic: "ANY" | "CONTROL" | "DIGIT" | "EOL" | "LOWER" | "UPPER"

$literal: '"' (escape | ~['"' CONTROL])+ '"'

$lowercase: LOWER (DIGIT | LOWER | UPPER)*

$note: "! " ~[CONTROL]*

$number: DIGIT+

$unicode: ('x' base16{2}) | ('u' base16{4}) | ('U' base16{8})

$uppercase: UPPER (DIGIT | LOWER | UPPER)*

