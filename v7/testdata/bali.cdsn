!>
................................................................................
.    Copyright (c) 2009-2025 Crater Dog Technologies.  All Rights Reserved.    .
................................................................................
.  DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.               .
.                                                                              .
.  This code is free software; you can redistribute it and/or modify it under  .
.  the terms of The MIT License (MIT), as published by the Open Source         .
.  Initiative. (See https://opensource.org/license/MIT)                        .
................................................................................
<!

!>
BALI DOCUMENT NOTATION
This document contains a formal definition of the Bali Document Notation™ (Bali)
using Crater Dog Syntax Notation™ (CDSN).

For more information on CDSN see the wiki at:
  - https://github.com/craterdog/go-syntax-notation/wiki

┌──────────────────────────────────────────────────────────────────────────────┐
│                               RULE DEFINITIONS                               │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$Document: Header? Component

$Header: comment

$Component: Entity Parameters? note?

$Entity:
    Element
    Series
    Collection
    Procedure

$Parameters: "(" Association* ")"

$Association: Primitive ":" Component

$Primitive:
    Element
    Series

$Element:
    angle
    boolean
    citation
    duration
    glyph
    moment
    number
    percentage
    probability
    resource

$Series:
    binary
    bytecode
    name
    narrative
    pattern
    quote
    symbol
    tag
    version

$Collection:
    Empty
    Range
    Attributes
    Items  ! Must be after ranges and attributes.

$Empty: "[" ":"? "]"

$Range: LeftBracket Primitive ".." Primitive RightBracket

$LeftBracket:
    "["
    "("

$RightBracket:
    "]"
    ")"

$Attributes: "[" Association+ "]"

$Items: "[" Component+ "]"

$Procedure: "{" Line* "}"

$Line:
    Annotation
    Statement

$Annotation: comment

$Statement: MainClause OnClause? note?

$MainClause:
    Flow
    Induction
    Messaging
    Repository

$OnClause: "on" Failure MatchingClause+

$MatchingClause: "matching" Template "do" Procedure

$Failure: symbol

$Template: Expression

$Flow:
    IfClause
    SelectClause
    WhileClause
    WithClause
    ContinueClause
    BreakClause
    ReturnClause
    ThrowClause

$Induction:
    DoClause
    LetClause

$Messaging:
    PostClause
    RetrieveClause
    AcceptClause
    RejectClause
    PublishClause

$Repository:
    CheckoutClause
    SaveClause
    DiscardClause
    NotarizeClause

$IfClause: "if" Condition "do" Procedure

$Condition: Expression

$SelectClause: "select" Target MatchingClause+

$Target:
    Function
    Method
    Subcomponent
    Value  ! This must be last since others also begin with an identifier.

$Function: identifier "(" Argument* ")"

$Argument:
    Value
    Primitive

$Value: identifier

$Method: identifier Invoke identifier "(" Argument* ")"

$Invoke:
    synchronous
    asynchronous

$Subcomponent: identifier "[" Index+ "]"

$Index:
    Value
    Primitive

$WhileClause: "while" Condition "do" Procedure

$WithClause: "with" "each" Variable "in" Sequence "do" Procedure

$Variable: symbol

$Sequence: Expression

$ContinueClause: "continue" "loop"

$BreakClause: "break" "loop"

$ReturnClause: "return" Result

$Result: Expression

$ThrowClause: "throw" Exception

$Exception: Expression

$DoClause: "do" Invocation

$Invocation:
    Function
    Method

$LetClause: "let" Recipient Assignment Expression

$Assignment:
    ":="
    "?="
    "+="
    "-="
    "*="
    "/="

$Recipient:
    Variable
    Subcomponent

$PostClause: "post" Message "to" Bag

$Message: Expression

$Bag: Expression

$RetrieveClause: "retrieve" Recipient "from" Bag

$AcceptClause: "accept" Message

$RejectClause: "reject" Message

$PublishClause: "publish" Event

$Event: Expression

$CheckoutClause: "checkout" Recipient AtLevel? "from" Cited

$AtLevel: "at" "level" Expression

$Cited: Expression

$SaveClause: "save" Draft "as" Cited

$Draft: Expression

$DiscardClause: "discard" Draft

$NotarizeClause: "notarize" Draft "as" Cited

$Expression: Subject Predicate*

$Subject:
    Component
    Subcomponent
    Precedence
    Referent
    Complement
    Inversion
    Magnitude
    Function
    Method
    Value  ! This must be last since others also begin with an identifier.

$Predicate: Operation Expression

$Operation:
    Textual
    Logic
    Arithmetic
    Comparison

$Textual:
    "&"
    "is"
    "matches"

$Logic:
    "and"
    "san"
    "ior"
    "xor"

$Arithmetic:
    plus
    dash
    star
    slash
    modulo
    caret

$Comparison:
    less
    equal
    more

$Precedence: "(" Expression ")"

$Referent: "@" Indirect

$Indirect:
    Component
    Subcomponent
    Referent
    Function
    Method
    Value  ! This must be last since others also begin with an identifier.

$Complement: "not" Logical

$Logical:
    Component
    Subcomponent
    Precedence
    Referent
    Complement
    Function
    Method
    Value  ! This must be last since others also begin with an identifier.

$Inversion: Inverse Numerical

$Inverse:
    dash
    slash
    star

$Numerical:
    Component
    Subcomponent
    Precedence
    Referent
    Inversion
    Magnitude
    Function
    Method
    Value  ! This must be last since others also begin with an identifier.

$Magnitude: "|" Numerical "|"

!>
┌──────────────────────────────────────────────────────────────────────────────┐
│                            EXPRESSION DEFINITIONS                            │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$angle: '~' ('0' | AMPLITUDE)

$binary: "'>" (BASE64 | SPACE | EOL)* "<'"

$boolean: "false" | "true"

$caret: '^'

$glyph: "'" (ESCAPE | ~[CONTROL]) "'"  ! Must be declared before bytecode.

$bytecode: "'" INSTRUCTION (SPACE INSTRUCTION)* "'"

$citation: name '@' version

$comment: "!>" (ANY | EOL)* "<!"  ! Chooses the shortest match.

$duration: '~' SIGN? 'P' (WEEKS | (YEARS? MONTHS? DAYS? ('T' HOURS? MINUTES? SECONDS?)?))

$equal: '='

$modulo: '%'

$moment: '<' SIGN? YEAR ('-' MONTH ('-' DAY ('T' HOUR (':' MINUTE (':' SECOND FRACTION?)?)?)?)?)? '>'

$name: ('/' identifier)+

$narrative: '"' '>' (ANY | EOL)* '<' '"'  ! Chooses the shortest match.

$note: "! " ~[CONTROL]*

$pattern: "none" | REGEX | "any"

$percentage: REAL '%'

$probability: 'p' ('0' FRACTION? | "1")

$quote: '"' CHARACTER* '"'

$number: POLAR | RECTANGULAR | IMAGINARY | REAL  ! Must be after other numerics.

$resource: '<' SCHEME ':' ("//" AUTHORITY)? '/' PATH ('?' QUERY)? ('#' FRAGMENT)? '>'

$symbol: '$' identifier

$tag: '#' BASE32+

$version: 'v' ORDINAL ('.' ORDINAL)*

$identifier: LETTER (LETTER | DIGIT)*  ! Must be last alphanumeric expression.

$synchronous: "<-"  ! Must be after other expressions beginning with "<-".

$asynchronous: "<~"

$less: '<'  ! Must be after all other expressions beginning with "<".

$more: '>'

$plus: '+'  ! Must be after all other expressions beginning with an operator.

$dash: '-'  ! Must be after all other expressions beginning with an operator.

$star: '*'  ! Must be after all other expressions beginning with an operator.

$slash: '/'  ! Must be after all other expressions beginning with an operator.

!>
┌──────────────────────────────────────────────────────────────────────────────┐
│                             FRAGMENT DEFINITIONS                             │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$ALPHA: ['A'..'Z' 'a'..'z']

$ALPHANUMERIC: ALPHA | BASE10

$AMPLITUDE: ('0' FRACTION | ORDINAL FRACTION? | TRANSCENDENTAL) EXPONENT?

$AUTHORITY: ~['/' CONTROL]+

$BASE10: ['0'..'9']

$BASE16: BASE10 | ['a'..'f']

$BASE32: BASE10 | ['A'..'D' 'F'..'H' 'J'..'N' 'P'..'T' 'V'..'Z']

$BASE64: ALPHANUMERIC | ['+' '/']

$CHARACTER: ESCAPE | '\' '"' | ~['"' CONTROL]

$DAY: (['0'..'2'] ['1'..'9']) | ('3' ['0'..'1'])

$DAYS: TIMESPAN 'D'

$ESCAPE: '\' (UNICODE | ['a' 'b' 'f' 'n' 'r' 't' 'v' '\'])

$EXPONENT: 'E' SIGN? ORDINAL

$FLOAT: SIGN? AMPLITUDE

$FRACTION: '.' BASE10+

$FRAGMENT: ~['>' CONTROL]*

$HOUR: (['0'..'1'] ['0'..'9']) | ('2' ['0'..'3'])

$HOURS: TIMESPAN 'H'

$IMAGINARY: FLOAT 'i'

$INFINITY: SIGN? ("infinity" | '∞')

$INSTRUCTION: BASE16{4}

$LETTER: LOWER | UPPER

$MINUTE: ['0'..'5'] ['0'..'9']

$MINUTES: TIMESPAN 'M'

$MONTH: ('0' ['1'..'9']) | ('1' ['0'..'2'])

$MONTHS: TIMESPAN 'M'

$ORDINAL: ['1'..'9'] BASE10*

$PATH: ~['?' '#' '>' CONTROL]*

$POLAR: AMPLITUDE "e^" ('~' ('0' | AMPLITUDE))? 'i'

$QUERY: ~['#' '>' CONTROL]*

$REAL: FLOAT | '0' | INFINITY | UNDEFINED

$RECTANGULAR: SIGN? AMPLITUDE SIGN AMPLITUDE 'i'

$REGEX: '"' CHARACTER+ '"' '?'

$SCHEME: ALPHA (ALPHANUMERIC | '+' | '-' | '.')*

$SECOND: (['0'..'5'] ['0'..'9']) | ('6' ['0'..'1'])

$SECONDS: TIMESPAN 'S'

$SIGN: '+' | '-'

$SPACE: ' '

$TIMESPAN: '0' | (ORDINAL FRACTION?)

$TRANSCENDENTAL: "e" | "pi" | 'π' | "tau" | 'τ' | "phi" | 'φ'

$UNDEFINED: "undefined"

$UNICODE: ('u' BASE16{4}) | ('U' BASE16{8})

$WEEKS: TIMESPAN 'W'

$YEAR: '0' | ORDINAL

$YEARS: TIMESPAN 'Y'

